allprojects {
    repositories {
        jcenter()
        google()
    }

    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    version = '0.0.103'
}

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0"
        classpath "com.github.dcendents:android-maven-gradle-plugin:2.0"
    }
}

subprojects {


}

configure(allprojects) { project ->
    if (!project.name.contains("android")) {

        apply plugin: "java"


        compileJava {
            sourceCompatibility = 1.8
            targetCompatibility = 1.8
            options.encoding = 'UTF-8'


        }
        repositories {
            mavenCentral()
        }

        dependencies {
            compile 'com.google.code.findbugs:jsr305:3.0.2'
            compile 'org.slf4j:slf4j-api:1.7.25'
        }


    }

    def artifactProjects = ["hcf-core", "hcf-context", "hcf-api-spring", "hcf-codegen"]

    if (artifactProjects.contains(project.name)) {
        group = "org.homunculusframework"


        apply plugin: 'maven-publish'

        apply plugin: 'com.jfrog.bintray'

        // custom tasks for creating source/javadoc jars
        task sourcesJar(type: Jar, dependsOn: classes) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }

        task javadocJar(type: Jar, dependsOn: javadoc) {
            classifier = 'javadoc'
            from javadoc.destinationDir
        }

        // add javadoc/source jar tasks as artifacts
        artifacts {
            archives sourcesJar, javadocJar
        }

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    if (plugins.hasPlugin('war')) {
                        from components.web
                    } else {
                        from components.java
                    }

                    artifact sourcesJar
                    artifact javadocJar
                }
            }
        }

        //nano ~/.gradle/gradle.properties
        bintray {
            user = bintray_user
            key = bintray_key
            publications = ['mavenJava']
            publish = true
            //configurations = ['archives']
            pkg {
                websiteUrl = 'http://homunculus.io/'
                repo = 'homunculus'
                name = project.name
                userOrg = 'worldiety'
                publicDownloadNumbers = true
                licenses = ['Apache-2.0']
                vcsUrl = 'https://github.com/worldiety/homunculus.git'
                version {
                    name = project.version
                }
            }
        }

    }

}





